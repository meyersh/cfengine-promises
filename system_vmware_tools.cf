bundle agent install_vmware_tools 
{
      # This bundle uses the repo packages at for redhat-based
      # distros:
      # http://packages.vmware.com/tools/esx/5.1latest/repos/

  vars:
      # This is the path from the vmware packages site.
      "vmware_version" string => "5.1latest";
      "package_name"   string => "vmware-tools-esx-nox";
      
    ubuntu_12::
      "ubuntu_name"    string => "precise";

    ubuntu_11::
      "ubuntu_name"    string => "oneiric";

    ubuntu::
      "sources" slist => 
      {
        "deb http://packages.vmware.com/tools/esx/$(vmware_version)/ubuntu $(ubuntu_name) main restricted",
        "deb-src http://packages.vmware.com/tools/esx/$(vmware_version)/ubuntu $(ubuntu_name) main restricted",
      };

    redhat_6|centos_6::
      "redhat_version" string => "rhel6";
      "redhat_flavor"    string => "RHEL6";

    redhat_5|centos_5::
      "redhat_version" string => "rhel5";      
      "redhat_flavor"    string => "RHEL5";

  classes:
    linux::

      "vmware_tar_install_present" expression => fileexists("/usr/bin/vmware-uninstall-tools.pl");
      
      # Define "vmware_vm" class when VMware is in the Product Name.
      "vmware_vm" expression => returnszero("/usr/sbin/dmidecode | /bin/grep 'Product Name: VMware' > /dev/null",
                                            "useshell");

  files:

    vmware_vm.ubuntu::
      # Desired file contents:
      # deb http://packages.vmware.com/tools/esx/$(vmware_version)/ubuntu $(ubuntu_name) main restricted
      # deb-src http://packages.vmware.com/tools/esx/$(vmware_version)/ubuntu $(ubuntu_name) main restricted

      "/etc/apt/sources.list.d/vmware-ubuntu.list"
      handle => "vmware_tools_repo",
      create => "true",
      edit_line => insert_lines("$sources");

  packages:
    vmware_vm.redhat::
      # http://packages.vmware.com/tools/esx/5.1latest/repos/vmware-tools-repo-RHEL5-9.0.1-2.i686.rpm
      "vmware-tools-repo"
      handle => "vmware_tools_repo",
      package_policy => "add",
      package_version => "$(redhat_flavor)-9.0.1-2",
      package_architectures => { "$(sys.arch)" },
      package_method => rpm_filebased("http://packages.vmware.com/tools/esx/$(vmware_version)/repos");
      
    vmware_vm.(redhat|ubuntu)::
      "$(package_name)"
      depends_on => {"vmware_tools_repo"},
      comment => "Ensure that vmware-tools are installed.",
      handle => "vmware_tools_installed",
      package_policy => "add",
      package_method => generic;
      
    linux::
      "dmidecode"
      handle => "dmidecode_installed",
      comment => "dmidecode is required to determine if a machine is a VMWare VM.",
      package_policy => "add",
      package_method => generic;
      
  commands:
    vmware_vm.redhat.import_keys::
      "/bin/rpm --import $(gpgkeys)";

    vmware_vm.vmware_tar_install_present.never::
      "/usr/bin/vmware-uninstall-tools.pl"
      depends_on => {"vmware_tools_installed"};
}
